program -> decl_list
---
decl_list -> decl decl_list
           | decl
---
decl -> var_decl
      | fun_decl
---
var_decl -> type_spec ID SEMICOLON
          | type_spec ID LEFT_SQUARE_B RIGHT_SQUARE_B SEMICOLON
---
type_spec -> VOID
           | BOOL
           | INT
           | FLOAT
---
fun_decl -> type_spec ID LEFT_ROUND_B params RIGHT_ROUND_B compound_stmt
---
params -> param_list
        | VOID
        | NULL
---
param_list -> param_list COMMA param
            | param
---
param -> type_spec ID
       | type_spec ID LEFT_SQUARE_B RIGHT_SQUARE_B
---
stmt_list -> stmt_list stmt
           | NULL
---
stmt -> expr_stmt
      | compound_stmt
      | if_stmt
      | while_stmt
      | return_stmt
      | break_stmt
---
expr_stmt -> expr SEMICOLON
           | SEMICOLON
---
while_stmt -> WHILE LEFT_ROUND_B expr RIGHT_ROUND_B stmt
---
compound_stmt -> LEFT_CURLY_B local_decls stmt_list RIGHT_CURLY_B
---
local_decls -> local_decls local_decl
             | NULL
---
local_decl -> type_spec ID SEMICOLON
            | type_spec ID LEFT_SQUARE_B RIGHT_SQUARE_B SEMICOLON
---
if_stmt -> IF LEFT_ROUND_B expr RIGHT_ROUND_B stmt
         | IF LEFT_ROUND_B expr RIGHT_ROUND_B stmt ELSE stmt
---
return_stmt -> RETURN SEMICOLON
             | RETURN expr SEMICOLON
---
arg_list -> arg_list COMMA expr
          | expr
---
args -> arg_list
      | NULL
---
expr -> ID = expr6
      | ID LEFT_SQUARE_B expr6 RIGHT_SQUARE_B = expr6
      | expr6
      
expr6 -> expr6 OR expr5
       | expr5
---
expr5 -> expr5 AND expr4
       | expr4
---
expr4 -> expr3 EQUAL expr3
      | expr3 NOT_EQUAL expr3
      | expr3 LESS_EQ expr3
      | expr3 LESSTHAN expr3
      | expr3 GREAT_EQ expr3
      | expr3 GREATERTHAN expr3
      | expr3
---
expr3 -> expr3 PLUS expr2
      | expr3 MINUS expr2
      | expr2
---
expr2 -> expr2 ASTERICK expr1
      | expr2 DIVIDE expr1
      | expr2 MOD expr1
      | expr1
---
expr1 -> NOT expr1
      | MINUS expr1
      | PLUS expr1
      | LEFT_ROUND_B expr RIGHT_ROUND_B
      | ID
      | ID LEFT_SQUARE_B expr RIGHT_SQUARE_B
      | ID LEFT_ROUND_B args RIGHT_ROUND_B
      | ID DOT size
      | TRUE
      | FALSE
      | INTEGRAL_LITERAL
      | FLOAT_LITERAL
      | NEW type_spec LEFT_SQUARE_B expr RIGHT_SQUARE_B
---